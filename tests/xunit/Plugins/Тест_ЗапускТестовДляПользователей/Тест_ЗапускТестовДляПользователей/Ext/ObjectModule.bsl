Перем КонтекстЯдра;
Перем Утверждения;
Перем МенеджерЗапуска1С;
Перем Файлы;

//{ основная процедура для юнит-тестирования xUnitFor1C
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
	МенеджерЗапуска1С = КонтекстЯдра.Плагин("ЗапускТестовДляПользователей");
	Файлы = КонтекстЯдра.Плагин("Файлы");
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов, КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	НаборТестов.Добавить("ТестДолжен_ЗапуститьПользователя");
КонецПроцедуры

//}

//{ Блок юнит-тестов

Процедура ПередЗапускомТеста() Экспорт
	НачатьТранзакцию();
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	Если ТранзакцияАктивна() Тогда
	    ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры

Процедура ТестДолжен_ЗапуститьПользователя() Экспорт
	ТипКлиента = МенеджерЗапуска1С.ВозможныеТипыКлиентов().ТолстыйОФ;
	ОписаниеПользователя = Новый Структура("Логин,Пароль", "User", "");
	ПутьТестов = Файлы.КаталогЗапускателяТестов() + "/tests/xunit/plugins/Тесты_СтроковыеУтилиты.epf";
	
	ОписаниеРезультата = МенеджерЗапуска1С.ЗапуститьТестДляПользователя(
		ОписаниеПользователя, ПутьТестов, ТипКлиента);
	
	КодВозврата = ОписаниеРезультата.КодВозврата;
	ПутьОтчетаJUnit = ОписаниеРезультата.ПутьОтчетаJUnit;
	СтатусВыполнения = ОписаниеРезультата.СтатусВыполнения;
	ТекстЛогФайла = ОписаниеРезультата.ТекстЛогФайла;
	ТекстОтчетаJUnit = ОписаниеРезультата.ТекстОтчетаJUnit;
	
	Утверждения.ПроверитьРавенство(КодВозврата, 0, "КодВозврата");
	
	Утверждения.ПроверитьЗаполненность(ПутьОтчетаJUnit, "ПутьОтчетаJUnit");
	Файл = Новый Файл(ПутьОтчетаJUnit);
	Утверждения.ПроверитьИстину(Файл.Существует(), "Файл не существует - ПутьОтчетаJUnit: " + Файл.ПолноеИмя);
	
	Утверждения.ПроверитьИстину(СтатусВыполнения, "СтатусВыполнения");
	
	Утверждения.ПроверитьВхождение(ТекстЛогФайла, "набор тестов Тесты_СтроковыеУтилиты", "ТекстЛогФайла");
	Утверждения.ПроверитьВхождение(ТекстЛогФайла, "набор тестов Функции парсинга текста и подстановки параметров", "ТекстЛогФайла");
	Утверждения.ПроверитьВхождение(ТекстЛогФайла, "тест Проверка работы функции СократитьДвойныеКавычки", 
		"ТекстЛогФайла");
	
	Утверждения.ПроверитьВхождение(ТекстОтчетаJUnit, 
		"<testsuite name=""Тесты_СтроковыеУтилиты"">", "ТекстОтчетаJUnit");
	Утверждения.ПроверитьВхождение(ТекстОтчетаJUnit, 
		"<testsuite name=""Функции парсинга текста и подстановки параметров"">", "ТекстОтчетаJUnit");
	Утверждения.ПроверитьВхождение(ТекстОтчетаJUnit, 
		"<testcase classname=""Функции преобразования текста и символов"" name=""Проверка работы функции ФорматированнаяСтрока""", 
		"ТекстОтчетаJUnit");
	
КонецПроцедуры

//}