#Использовать logos
#Использовать 1commands
#Использовать fs
#Использовать cmdline

Перем ВозможныеКоманды;
Перем Лог;
Перем ЭтоWindows;
Перем Бинарники1СХранятсяРядомСИсходникам;

Процедура ИнициализацияОкружения()

	СистемнаяИнформация = Новый СистемнаяИнформация;
	ЭтоWindows = Найти(ВРег(СистемнаяИнформация.ВерсияОС), "WINDOWS") > 0;

	Лог = Логирование.ПолучитьЛог(ИмяЛогаОбработчика());
	// Лог.УстановитьУровень(УровниЛога.Отладка);
	Лог.УстановитьРаскладку(ЭтотОбъект);
	
	ПарсерАргументовКоманднойСтроки = Новый ПарсерАргументовКоманднойСтроки();
	ПарсерАргументовКоманднойСтроки.ДобавитьПараметрФлаг("--useBuildFolderForBinaries", 
	"Использовать каталог ""./build"" для работы с бинарными файлами (внешние обработки/отчеты)
	|В конфигураторе работаем с файлами из этого каталога
	|А в исходники попадают файлы из корневого каталога");

	ПарсерАргументовКоманднойСтроки.ДобавитьПараметр("Каталог", 
		"Каталог, где находятся исходники для сборки бинарных файлов (epf,erf)");
	Аргументы = ПарсерАргументовКоманднойСтроки.Разобрать(АргументыКоманднойСтроки);
	
	ОдинКаталог = Аргументы["Каталог"];
	Лог.Информация("Получили каталог для сборки %1", ""+ОдинКаталог);

	Бинарники1СХранятсяРядомСИсходникам = Не Аргументы["--useBuildFolderForBinaries"];
	Лог.Информация("Указан вариант хранения бинарных внешних файлов (epf,erf) %1", 
	?(Бинарники1СХранятсяРядомСИсходникам, "- в корневом каталоге", "- в каталоге ./build"));

	МассивПутей = Новый Массив();

	Если Не ПустаяСтрока(ОдинКаталог) Тогда
		
		МассивПутей.Добавить(ОдинКаталог);
		
	Иначе
		
		МассивПутей.Добавить("./epf");
		МассивПутей.Добавить("./features");
		МассивПутей.Добавить("./vendor");
		МассивПутей.Добавить("./plugins");
		МассивПутей.Добавить("./lib/featurereader");
		МассивПутей.Добавить("./locales");
		
	КонецЕсли;

	КаталогПроекта = КаталогПроекта();
	Для каждого Элемент из МассивПутей Цикл
		ЗапуститьОбработку(Элемент, КаталогПроекта);
	КонецЦикла;

КонецПроцедуры

Процедура ЗапуститьОбработку(Знач Путь, Знач КаталогПроекта)
	Перем ПодкаталогСборки;
	ПодкаталогСборки = ?(Бинарники1СХранятсяРядомСИсходникам, "", "build/");

	Файл = Новый Файл(Путь);
	Если Файл.ИмяБезРасширения = "epf" И Файл.ЭтоКаталог() Тогда 
		ЧтоИКуда = СтрШаблон("./epf ./%1", ПодкаталогСборки);
	Иначе
		ОтносительныйПутьРодителя = ФС.ОтносительныйПуть(КаталогПроекта, Файл.Путь);

		Если ОтносительныйПутьРодителя = "epf" Тогда
			ЧтоИКуда = СтрШаблон("epf/%1 ./%2", Файл.ИмяБезРасширения, ПодкаталогСборки);
		Иначе
			ЧтоИКуда = СтрШаблон("%1 ./%2%1", Путь, ПодкаталогСборки);
		КонецЕсли;
	КонецЕсли;

	ШаблонЗапуска = СтрШаблон("oscript ./tools/runner.os compileepf %1 --ibname /F./build/ibservice", ЧтоИКуда);
	ИсполнитьКоманду(ШаблонЗапуска);
	
КонецПроцедуры

Функция ИсполнитьКоманду(Знач СтрокаВыполнения)
	
	Команда = Новый Команда;
	Команда.ПоказыватьВыводНемедленно(Истина);
	
	Команда.ДобавитьЛогВыводаКоманды(ИмяЛогаОбработчика());

	Команда.УстановитьПравильныйКодВозврата(0);

	Лог.Информация(СтрокаВыполнения);
	Команда.УстановитьСтрокуЗапуска(СтрокаВыполнения);
	Команда.Исполнить();

	Возврат Команда.ПолучитьВывод();
	
КонецФункции

Функция ФайлНаходитсяВКорнеПроекта(Знач ПутьФайла, Знач ПодкаталогСборки)
	Если ЗначениеЗаполнено(ПодкаталогСборки) Тогда
		ИмяФайла = Новый Файл(ПутьФайла).Имя;
		ПутьФайла = СтрШаблон("./%1%2", ПодкаталогСборки, ИмяФайла);
	КонецЕсли;

	ПолныйПуть = ФС.ПолныйПуть(ПутьФайла);
	ПолныйПутьКаталогаПроекта = ФС.ПолныйПуть(КаталогПроекта());
	ОтносительныйПуть = ФС.ОтносительныйПуть(ПолныйПутьКаталогаПроекта, ПолныйПуть);

	Возврат ОтносительныйПуть = ".";
КонецФункции

Функция КаталогПроекта()
	ФайлИсточника = Новый Файл(ТекущийСценарий().Источник);
	Возврат ОбъединитьПути(ФайлИсточника.Путь, "..");
КонецФункции

Функция ИмяЛогаОбработчика()
	Возврат "oscript.app.vanessa-runner-init";
КонецФункции

Функция Форматировать(Знач Уровень, Знач Сообщение) Экспорт
	
	Если Не ЭтоWindows Тогда
		Сообщение = СтрЗаменить(Сообщение, "No bp log location saved, using default.", "");
		Если ПустаяСтрока(Сообщение) Тогда
			Возврат "";
		КонецЕсли;
	КонецЕсли;

	Возврат СтрШаблон("%1: %2 - %3", ТекущаяДата(), УровниЛога.НаименованиеУровня(Уровень), Сообщение);

КонецФункции

ИнициализацияОкружения();
